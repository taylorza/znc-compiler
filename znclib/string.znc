#ifndef STRING__
const byte STRING__=1;

char* strcpy(char *dst, char *src) __asm__ {
  push ix
  ld ix,0
  add ix,sp

  ld d,(ix+7)
  ld e,(ix+6)
  
  ld h,(ix+5)
  ld l,(ix+4) 

.n
  cp (hl)
  ldi
  jr nz,.n

.x
  ex de,hl
  pop ix
  ret
}

int strlen(char *s) __asm__ {
  push ix
  ld ix,0
  add ix,sp

  ld h,(ix+5)
  ld l,(ix+4) 

  xor a
  ld c,a
  ld b,a

  cpir
  ld hl, 0xffff
  sbc hl,bc

  pop ix
  ret
}

int strcmp(char *s1, char *s2) __asm__ {
  push ix
  ld ix,0
  add ix,sp

  ld d,(ix+7)
  ld e,(ix+6)
  
  ld h,(ix+5)
  ld l,(ix+4) 

.l
  ld a,(de)
  cpi
  jr nz,.n
  inc de
  or a
  jr nz,.l
  
  dec de
  ld h,a
  ld l,a
  jr .x

.n
  dec hl
  sub (hl)
  ld h,a

.x
  pop ix
  ret
}

char* strcat(char *s1, char *s2) __asm__ {
  push ix
  ld ix,0
  add ix,sp

  ld d,(ix+7)
  ld e,(ix+6)
  
  ld h,(ix+5)
  ld l,(ix+4) 
 
  push de
.z
  ld a,(de)
  or a
  jr z,.l
  inc de
  jr .z

.l
  cp (hl)
  ldi
  jr nz,.l
  pop hl

  pop ix
  ret
}

char* strstr(char *s1, char *s2) __asm__ {
  push ix
  ld ix,0
  add ix,sp

  ld h,(ix+7)
  ld l,(ix+6)
  
  ld d,(ix+5)
  ld e,(ix+4)

  ld a,(de)
  or a
  jr z,.ret

.l1
  ld a,(hl)
  or a
  jr z,.no

  ld a,(de)
  cp (hl)
  jr nz,.next

  push hl
  push de
  inc hl
  inc de
.l2
  ld a,(de)
  or a
  jr z,.yes
  
  cp (hl)
  jr nz,.rest
   
  inc hl
  inc de
  jr .l2
.rest
  pop de
  pop hl
.next
  inc hl
  jr .l1

.no
  ld hl,0
  pop ix
  ret

.yes
  pop de
  pop hl
.ret
  pop ix
  ret
}
#endif